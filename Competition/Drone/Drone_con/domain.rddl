///////////////////////////////////////////////////////////////////////////////
//
// Drone RDDL domain
//
///////////////////////////////////////////////////////////////////////////////

domain simple_drone{

types {
    aircraft : object;

};

pvariables {

    CONTROLLABLE(aircraft) : { non-fluent, bool, default = false}; // which aircraft can be controlled

    // Bounds on the position of the aircraft
    MIN_X : { non-fluent, real, default = -50000.0};
    MAX_X : { non-fluent, real, default = 50000.0};
    MIN_Y : { non-fluent, real, default = -50000.0};
    MAX_Y : { non-fluent, real, default = 50000.0};
    MIN_Z : { non-fluent, real, default = -50000.0};
    MAX_Z : { non-fluent, real, default = 50000.0};

    GRAVITY : { non-fluent, real, default = 9.8};

    SCALE_FACTOR  : { non-fluent, real, default = 0.1 };          // time scale factor for dynamic equations

    // bounds on actions
    MIN_VEL(aircraft) : {non-fluent, real, default = -1.0};
    MAX_VEL(aircraft) : {non-fluent, real, default = 1.0};
    MIN_PHI(aircraft) : {non-fluent, real, default = -1.0};
    MAX_PHI(aircraft) : {non-fluent, real, default = 1.0};
    MIN_THETA(aircraft) : {non-fluent, real, default = -1.0};
    MAX_THETA(aircraft) : {non-fluent, real, default = 1.0};

    // goal position
    GOAL_X(aircraft) : {non-fluent, real, default = 100.0};
    GOAL_Y(aircraft) : {non-fluent, real, default = 100.0};
    GOAL_Z(aircraft) : {non-fluent, real, default = 100.0};
    
    // Coordinates
    pos_x(aircraft) : { state-fluent, real, default = 0.0 };
    pos_y(aircraft) : { state-fluent, real, default = 0.0 }; 
    pos_z(aircraft) : { state-fluent, real, default = 0.0 };

    // velocity
    vel(aircraft) : { state-fluent, real, default = 1.0 };

    // Control variables
    theta(aircraft) : { state-fluent, real, default = 0.0 };  // pitch
    phi(aircraft) : { state-fluent, real, default = 0.0 };    // roll
    psi(aircraft) : { state-fluent, real, default = 0.0 };    // yaw

    // actions
    set_vel(aircraft)  :  { action-fluent, real, default = 0.0 };
    set_phi(aircraft)  :  { action-fluent, real, default = 0.0 };
    set_theta(aircraft)  :  { action-fluent, real, default = 0.0 };
   
};

cpfs {

    // position changes for each time step
    pos_x'(?a) = pos_x(?a) + SCALE_FACTOR * vel(?a) * cos[psi(?a)];
    pos_y'(?a) = pos_y(?a) + SCALE_FACTOR * vel(?a) * sin[psi(?a)];
    pos_z'(?a) = pos_z(?a) + SCALE_FACTOR * vel(?a) * sin[theta(?a)];

    // velocity
    vel'(?a) = vel(?a) + SCALE_FACTOR * max[min[set_vel(?a), MAX_VEL(?a)], MIN_VEL(?a)];

    // angle changes
    phi'(?a) = phi(?a) + SCALE_FACTOR * max[min[set_phi(?a), MAX_PHI(?a)], MIN_PHI(?a)];
    theta'(?a) = theta(?a) + SCALE_FACTOR * max[min[set_theta(?a), MAX_THETA(?a)], MIN_THETA(?a)];
    psi'(?a) = psi(?a) + SCALE_FACTOR * (tan[phi(?a)] / vel(?a)) * GRAVITY;

};

reward = sum_{?a : aircraft} [ 1 / (sqrt[pow[(pos_x(?a) - GOAL_X(?a)),2] + 
                                   pow[(pos_y(?a) - GOAL_Y(?a)),2] +
                                   pow[(pos_z(?a) - GOAL_Z(?a)),2]]) ];

action-preconditions {
    // boundries of the aircarft locations
    forall_{?a : aircraft}[ pos_x(?a) <= MAX_X ^ pos_x(?a) >= MIN_X ^
                            pos_y(?a) <= MAX_Y ^ pos_y(?a) >= MIN_Y ^
                            pos_z(?a) <= MAX_Z ^ pos_x(?a) >= MIN_Z ];
    // boundries for set vel 
    forall_{?a : aircraft}[ set_vel(?a) <= MAX_VEL(?a) ^ set_vel(?a) >= MIN_VEL(?a)];
    forall_{?a : aircraft}[ set_phi(?a) <= MAX_PHI(?a) ^ set_phi(?a) >= MIN_PHI(?a)];
    forall_{?a : aircraft}[ set_theta(?a) <= MAX_THETA(?a) ^ set_theta(?a) >= MIN_THETA(?a)];
};

}